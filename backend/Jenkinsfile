podTemplate(label: 'mypod', containers: [
    containerTemplate(name: 'git', image: 'alpine/git', ttyEnabled: true, command: 'cat'),
    containerTemplate(name: 'maven', image: 'maven:3.6.3-openjdk-15-slim', command: 'cat', ttyEnabled: true),
    containerTemplate(name: 'docker', image: 'docker', command: 'cat', ttyEnabled: true),
    containerTemplate(name: 'kubectl', image: 'lachlanevenson/k8s-kubectl:v1.20.1', command: 'cat', ttyEnabled: true)
  ],
  volumes: [
    hostPathVolume(mountPath: '/var/run/docker.sock', hostPath: '/var/run/docker.sock'),
  ]
  ) {
    node('mypod') {
        stage('Clone repository') {
            container('git') {
                checkout scm
            }
        }

        stage('Build') {
            container('maven') {
                configFileProvider([configFile(fileId: 'maven-settings', variable: 'MAVEN_GLOBAL_SETTINGS')]) {
                    sh 'mvn -f backend -B -gs $MAVEN_GLOBAL_SETTINGS -DskipTests clean package'
                }
            }
        }

        stage('Test') {
            container('maven') {
                configFileProvider([configFile(fileId: 'maven-settings', variable: 'MAVEN_GLOBAL_SETTINGS')]) {
                    sh 'mvn -f backend -B -gs $MAVEN_GLOBAL_SETTINGS test'
                }
            }
        }

        stage('Build docker image') {
            container('docker') {
                ['api', 'transaction-service', 'transaction-import-service'].each { service ->
                    dir("backend/${service}") {
                        sh "docker build -t ${env.DOCKER_REGISTRY_PREFIX}/${service}:${env.BUILD_ID} -t ${env.DOCKER_REGISTRY_PREFIX}/${service}:latest ."
                        sh "docker push ${env.DOCKER_REGISTRY_PREFIX}/${service}:${env.BUILD_ID}"
                    }
                }
            }
        }

        stage('Deploy') {
            container('kubectl') {
                ['api', 'transaction-service', 'transaction-import-service'].each { service ->
                    dir("backend/${service}") {
                        sh "sed -i 's/\$DOCKER_REGISTRY_PREFIX/${env.DOCKER_REGISTRY_PREFIX}/g' manifest.yaml"
                        sh "sed -i 's/\$BUILD_ID/${env.BUILD_ID}/g' manifest.yaml"
                        sh "kubectl apply -f manifest.yaml --namespace=default"
                    }
                }
            }
        }
    }
}